COMPILER Tastier

/*
  First, let's declare all the types we're going to use.
*/

  enum TastierType : int {   // types for variables
    Undefined,
    Integer,
    Boolean,
    String,
    Character,
    Array,
    Record
  };

  enum TastierKind : int {  // kinds of symbol
    Var,
    Proc,
    Const
  };

/* using RecordVar = System.Tuple<string, int, int, int>
    * RecordVar.Item1 -> Member name
    * RecordVar.Item2 -> Type
    * RecordVar.Item3 -> Base type (only for arrays)
    * RecordVar.Item4 -> Level of indirection
*/

/* using Record = System.Tuple<int, System.Collections.Generic.List<RecordVar>>
    * Record.Item1 -> id of this record
    * Record.Item2 -> List of variables in this record
*/

Dictionary<string, Record> recordDeclarations = new Dictionary<string, Record>();

Record lookupRecordId(int id) {
  foreach (Record rec in recordDeclarations.Values) {
    if (rec.Item1 == id) {
      return rec;
    }
  }
  return null;
}

// Used to uniquely identify each record declared
int recordsDeclared = 0;

// Returns whether a member name is valid for a given record
bool memberExists(Record record, string member) {
  foreach (RecordVar variable in record.Item2) {
    if (variable.Item1 == member) {
      return true;
    }
  }
  return false;
}

/*
  You'll notice some type aliases, such as the one just below, are commented
  out. This is because C# only allows using-alias-directives outside of a
  class, while class-inheritance directives are allowed inside. So the
  snippet immediately below is illegal in here. To complicate matters
  further, the C# runtime does not properly handle class-inheritance
  directives for Tuples (it forces you to write some useless methods). For
  these reasons, the type aliases which alias Tuples can be found in
  Parser.frame, but they're documented in this file, with the rest.
*/

  //using Symbol = System.Tuple<string, int, int, int, int, int, int>;

/*
  A Symbol is a name with a type and a kind. The first int in the
  tuple is the kind, and the second int is the type. We'll use these to
  represent declared names in the program.

  For each Symbol which is a variable, we have to allocate some storage, so
  the variable lives at some address in memory. The address of a variable on
  the stack at runtime has two components. The first component is which
  stack frame it's in, relative to the current procedure. If the variable is
  declared in the procedure that's currently executing, then it will be in
  that procedure's stack frame. If it's declared in the procedure that
  called the currently active one, then it'll be in the caller's stack
  frame, and so on. The first component is the offset that says how many
  frames up the chain of procedure calls to look for the variable. The
  second component is simply the location of the variable in the stack frame
  where it lives.

  The third int in the symbol is the stack frame on which the variable
  lives, and the fourth int is the index in that stack frame. Since
  variables which are declared in the global scope aren't inside any
  function, they don't have a stack frame to go into. In this compiler, our
  convention is to put these variables at an address in the data memory. If
  the variable was declared in the global scope, the fourth field in the
  Symbol will be zero, and we know that the next field is an address in
  global memory, not on the stack.

  Procedures, on the other hand, are just sets of instructions. A procedure
  is not data, so it isn't stored on the stack or in memory, but is just a
  particular part of the list of instructions in the program being run. If
  the symbol is the name of a procedure, we'll store a -1 in the address
  field (5).

  When the program is being run, the code will be loaded into the machine's
  instruction memory, and the procedure will have an address there. However,
  it's easier for us to just give the procedure a unique label, instead of
  remembering what address it lives at. The assembler will take care of
  converting the label into an address when it encounters a JMP, FJMP or
  CALL instruction with that label as a target.

  To summarize:
    * Symbol.Item1 -> name
    * Symbol.Item2 -> kind
    * Symbol.Item3 -> type
    * Symbol.Item4 -> stack frame pointer
    * Symbol.Item5 -> variable's address in the stack frame pointed to by
                      Item4, -1 if procedure
    * Symbol.Item6 -> Base type (only for arrays)
    * Symbol.Item7 -> Level of indirection
*/

  class Scope : Stack<Symbol> {}

/*
  A scope contains a stack of symbol definitions. Every time we come across
  a new local variable declaration, we can just push it onto the stack. We'll
  use the position of the variable in the stack to represent its address in
  the stack frame of the procedure in which it is defined. In other words, the
  variable at the bottom of the stack goes at location 0 in the stack frame,
  the next variable at location 1, and so on.
*/

  //using Instruction = Tuple<string, string>;
  class Program : List<Instruction> {}

/*
  A program is just a list of instructions. When the program is loaded into
  the machine's instruction memory, the instructions will be laid out in the
  same order that they appear in this list. Because of this, we can use the
  location of an instruction in the list as its address in instruction memory.
  Labels are just names for particular locations in the list of instructions
  that make up the program.

  The first component of all instructions is a label, which can be empty.
  The second component is the actual instruction itself.

  To summarize:
    * Instruction.Item1 -> label
    * Instruction.Item2 -> the actual instruction, as a string
*/

Stack<Scope> openScopes = new Stack<Scope>();
Scope externalDeclarations = new Scope();

/*
  Every time we encounter a new procedure declaration in the program, we want
  to make sure that expressions inside the procedure see all of the variables
  that were in scope at the point where the procedure was defined. We also
  want to make sure that expressions outside the procedure do not see the
  procedure's local variables. Every time we encounter a procedure, we'll push
  a new scope on the stack of open scopes. When the procedure ends, we can pop
  it off and continue, knowing that the local variables defined in the
  procedure cannot be seen outside, since we've popped the scope which
  contains them off the stack.
*/

Program program = new Program();
Program header = new Program();

Stack<string> openProcedureDeclarations = new Stack<string>();

/*
  In order to implement the "shadowing" of global procedures by local procedures
  properly, we need to generate a label for local procedures that is different
  from the label given to procedures of the same name in outer scopes. See the
  test case program "procedure-label-shadowing.TAS" for an example of why this
  is important. In order to make labels unique, when we encounter a non-global
  procedure declaration called "foo" (for example), we'll give it the label
  "enclosingProcedureName$foo" for all enclosing procedures. So if it's at
  nesting level 2, it'll get the label "outermost$nextoutermost$foo". Let's
  make a function that does this label generation given the set of open
  procedures which enclose some new procedure name.
*/

string generateProcedureName(string name) {
  if (openProcedureDeclarations.Count == 0) {
    return name;
  } else {
    string temp = name;
    foreach (string s in openProcedureDeclarations) {
      temp = s + "$" + temp;
    }
    return temp;
  }
}

/*
  We also need a function that figures out, when we call a procedure from some
  scope, what label to call. This is where we actually implement the shadowing;
  the innermost procedure with that name should be called, so we have to figure
  out what the label for that procedure is.
*/

string getLabelForProcedureName(int lexicalLevelDifference, string name) {
  /*
     We want to skip <lexicalLevelDifference> labels backwards, but compose
     a label that incorporates the names of all the enclosing procedures up
     to that point. A lexical level difference of zero indicates a procedure
     defined in the current scope; a difference of 1 indicates a procedure
     defined in the enclosing scope, and so on.
  */
  int numOpenProcedures = openProcedureDeclarations.Count;
  int numNamesToUse = (numOpenProcedures - lexicalLevelDifference);
  string theLabel = name;

  /*
    We need to concatenate the first <numNamesToUse> labels with a "$" to
    get the name of the label we need to call.
  */

  var names = openProcedureDeclarations.Take(numNamesToUse);

  foreach (string s in names) {
      theLabel = s + "$" + theLabel;
  }

  return theLabel;
}

Stack<string> openLabels = new Stack<string>();
int labelSeed = 0;

string generateLabel() {
  return "L$"+labelSeed++;
}

/*
  Sometimes, we need to jump over a block of code which we're about to
  generate (for example, at the start of a loop, if the test fails, we have
  to jump to the end of the loop). Because it hasn't been generated yet, we
  don't know how long it will be (in the case of the loop, we don't know how
  many instructions will be in the loop body until we actually generate the
  code, and count them). In this case, we can make up a new label for "the
  end of the loop" and emit a jump to that label. When we get to the end of
  the loop, we can put the label in, so that the jump will go to the
  labelled location. Since we can have loops within loops, we need to keep
  track of which label is the one that we are currently trying to jump to,
  and we need to make sure they go in the right order. We'll use a stack to
  store the labels for all of the forward jumps which are active. Every time
  we need to do a forward jump, we'll generate a label, emit a jump to that
  label, and push it on the stack. When we get to the end of the loop, we'll
  put the label in, and pop it off the stack.
*/

Symbol _lookup(Scope scope, string name) {
  foreach (Symbol s in scope) {
      if (s.Item1 == name) {
        return s;
      }
  }
  return null;
}

Symbol lookup(Stack<Scope> scopes, string name) {
  int stackFrameOffset = 0;
  int variableOffset = 0;

  foreach (Scope scope in scopes) {
    foreach (Symbol s in scope) {
      if (s.Item1 == name) {
        return s;
      }
      else {
        variableOffset += 1;
      }
    }
    stackFrameOffset += 1;
    variableOffset = 0;
  }
  return null; // if the name wasn't found in any open scopes.
}

// Generic type error function used throughout the code
void TypeErr(TastierType expected, TastierType actual) {
  SemErr("expected type " + expected + ", got type " + actual);
}

// Emits a write instruction depending on the type of the expression
void EmitWrite(TastierType type) {
  if (type == TastierType.Integer) {
    program.Add(new Instruction("", "Write"));
  } else if (type == TastierType.String) {
    program.Add(new Instruction("", "WriteS"));
  } else if (type == TastierType.Character) {
    program.Add(new Instruction("", "WriteC"));
  } else {
    SemErr("integer, character or string type expected");
  }
}

/*
  Creates the array structure for an array initialiser
  Arrays are initialised at the end of memory backwards
  The initialiser [2][3] assuming the base address of 4093 will give in memory
  2 4090 4086 3 0 0 0 3 0 0 0 ........
  Each array has a length at the beginning and if it is a multidimensional array
  all members will be pointers to the next level, otherwise 0
*/
void AllocateArray(List<int> lengths) {
  // Keep reference to pointer to beginning of array structure
  int temp = globalStorePosition;
  _AllocateArray(lengths, new List<int>(){1});
  program.Add(new Instruction("", "Const " + temp));
}

void _AllocateArray(List<int> lengths, List<int> previousLengths) {
  if (lengths.Count == 0) {
    return;
  }
  int times = 0;
  int offset = -1;
  int start = globalStorePosition;
  int productOfPreviousLengths = previousLengths.Aggregate((acc, val) => acc * val);
  int current = lengths[0];
  int iterations = (current + 1) * productOfPreviousLengths;
  for (int i = 0; i < iterations; i++) {
    if ((start - globalStorePosition) % (current + 1) == 0) {
      // Store the length of the array at the beginning
      program.Add(new Instruction("", "Const " + current));
      program.Add(new Instruction("", "StoG " + globalStorePosition));
      offset++;
    } else if (lengths.Count > 1) {
      // Store the pointer to the array this location refers to
      int memoryDifference = (current * productOfPreviousLengths) + (times * lengths[1]) + productOfPreviousLengths - 1 - offset;
      program.Add(new Instruction("", "Const " + (globalStorePosition - memoryDifference)));
      program.Add(new Instruction("", "StoG " + globalStorePosition));
      times++;
    }
    globalStorePosition--;
  }
  lengths.RemoveAt(0);
  previousLengths.Add(current);
  _AllocateArray(lengths, previousLengths);
}

/*
  You may notice that when we use a LoadG or StoG instruction, we add 3 to
  the address of the item being loaded or stored. This is because the
  control and status registers of the machine are mapped in at addresses 0,
  1, and 2 in data memory, so we cannot use those locations for storing
  variables. If you want to load rtp, rbp, or rpc onto the stack to
  manipulate them, you can LoadG and StoG to those locations.
*/

// Strings and arrays will be stored at the end of data memory
int globalStorePosition = 4094;
/*--------------------------------------------------------------------------*/

CHARACTERS
letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
digit = "0123456789".
special = "?!@#$%^&*(){}[]<>:;,.~`|/\\'\"+=-_".
cr  = '\r'.
lf  = '\n'.
tab = '\t'.
quote = '"'.
singlequote = '\''.
space = ' '.
characterSet = space + letter + digit + tab + special.
stringCharacter = characterSet - '"'.
charCharacter = characterSet - '\''.

TOKENS
  ident  = letter {letter | digit}.
  number = digit {digit}.
  str = quote {stringCharacter} quote.
  character = singlequote charCharacter singlequote.

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab

PRODUCTIONS

/*------------------------------------------------------------------------*/

AddOp<out Instruction inst>
=                               (.  inst = new Instruction("", "Add"); .)
  ( '+'
  | '-'                         (.  inst = new Instruction("", "Sub"); .)
  ).

/*------------------------------------------------------------------------*/

Expr<out TastierType type>      (.  TastierType type1; Instruction inst; .)
= SimExpr<out type>
  [ RelOp<out inst>
    SimExpr<out type1>          (.
                                    if (type != type1) {
                                      TypeErr(type1, type);
                                    }
                                    else {
                                      program.Add(inst);
                                      type = TastierType.Boolean;
                                    }
                                .)
  ].

/*------------------------------------------------------------------------*/

Factor<out TastierType type>    (.  int n; Symbol sym; string name; .)
=                               (.  type = TastierType.Undefined; .)
  ( Ident<out name>             (.
                                    int arrayAccessDepth = 0;
                                    bool isExternal = false; //CS3071 students can ignore external declarations, since they only deal with compilation of single files.
                                    sym = lookup(openScopes, name);
                                    if (sym == null) {
                                      sym = _lookup(externalDeclarations, name);
                                      isExternal = true;
                                    }
                                .)
    ( '.' Ident<out name>       (.
                                    if ((TastierType) sym.Item3 != TastierType.Record) {
                                      TypeErr(TastierType.Record, (TastierType) sym.Item3);
                                    }
                                    Record rec = lookupRecordId(sym.Item7);
                                    if (memberExists(rec, name)) {
                                      sym = lookup(openScopes, "_" + sym.Item1 + "." + name);
                                    } else {
                                      SemErr(sym.Item1 + " does not contain member " + name);
                                    }
                                .)
    | { '[' Expr<out type>      (.
                                    if (type != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type);
                                    }
                                    arrayAccessDepth++;
                                .)
        ']'
      }
    )                           (.
                                    if (sym == null) {
                                      SemErr("reference to undefined variable " + name);
                                    } else if ((TastierType) sym.Item3 == TastierType.Record) {
                                      // If we are accessing a record push all of its values onto the stack
                                      type = TastierType.Record;
                                      Record rec = lookupRecordId(sym.Item7);
                                      for (int i = rec.Item2.Count - 1; i >= 0 ; i--) {
                                        RecordVar member = rec.Item2[i];
                                        Symbol memberSym = lookup(openScopes, "_" + sym.Item1 + "." + member.Item1);
                                        if (memberSym.Item4 == 0) {
                                          program.Add(new Instruction("", "LoadG " + (memberSym.Item5+3)));
                                        } else {
                                          int lexicalLevelDifference = Math.Abs(openScopes.Count - memberSym.Item4)-1;
                                          program.Add(new Instruction("", "Load " + lexicalLevelDifference + " " + memberSym.Item5));
                                        }
                                      }
                                    } else {
                                      type = (TastierType)sym.Item3;
                                      if ((TastierKind)sym.Item2 == TastierKind.Var || (TastierKind)sym.Item2 == TastierKind.Const) {
                                        if (sym.Item4 == 0) {
                                            if (isExternal) {
                                              program.Add(new Instruction("", "LoadG " + sym.Item1));
                                              // if the symbol is external, we load it by name. The linker will resolve the name to an address.
                                            } else {
                                              program.Add(new Instruction("", "LoadG " + (sym.Item5+3)));
                                            }
                                        } else {
                                            int lexicalLevelDifference = Math.Abs(openScopes.Count - sym.Item4)-1;
                                            program.Add(new Instruction("", "Load " + lexicalLevelDifference + " " + sym.Item5));
                                        }
                                      } else SemErr("variable expected");
                                      if (arrayAccessDepth > 0) {
                                        if (arrayAccessDepth == sym.Item7) {
                                          // If we provide enough dimensions the type should be the base type of the array
                                          // If not we will return "Array" indicating that it is a pointer to the next level of the array
                                          type = (TastierType) sym.Item6;
                                        } else if (arrayAccessDepth > sym.Item7) {
                                          SemErr("Too many dimensions specified");
                                        }
                                        program.Add(new Instruction("", "LoadArr " + arrayAccessDepth));
                                      }
                                    }
                                .)
  | number                      (.
                                    n = Convert.ToInt32(t.val);
                                    program.Add(new Instruction("", "Const " + n));
                                    type = TastierType.Integer;
                                .)
  | str                         (.
                                    // Remove quotes from the matched token
                                    string str = t.val.Trim(new Char[] {'"'});
                                    int temp = globalStorePosition;
                                    program.Add(new Instruction("", "Const " + str.Length));
                                    program.Add(new Instruction("", "StoG " + globalStorePosition--));
                                    foreach (char c in str) {
                                      program.Add(new Instruction("", "Const " + (int) c));
                                      program.Add(new Instruction("", "StoG " + globalStorePosition--));
                                    }
                                    // Pointer to our string
                                    program.Add(new Instruction("", "Const " + temp));
                                    type = TastierType.String;
                                .)
  | character                   (.
                                    // t.val[0] and t.val[2] will be our quotes. t.val[1] is our character
                                    char c = t.val[1];
                                    program.Add(new Instruction("", "Const " + (int) c));
                                    type = TastierType.Character;
                                .)
  | '-'
    Factor<out type>            (.
                                    if (type != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type);
                                      type = TastierType.Integer;
                                    }
                                    program.Add(new Instruction("", "Neg"));
                                    program.Add(new Instruction("", "Const 1"));
                                    program.Add(new Instruction("", "Add"));
                                .)
  | "true"                      (.  program.Add(new Instruction("", "Const " + 1)); type = TastierType.Boolean; .)
  | "false"                     (.  program.Add(new Instruction("", "Const " + 0)); type = TastierType.Boolean; .)
  ).

/*------------------------------------------------------------------------*/

Ident<out string name>
= ident                         (.  name = t.val; .).

/*------------------------------------------------------------------------*/

MulOp<out Instruction inst>
=                               (.  inst = new Instruction("", "Mul"); .)
  ( '*'
  | '/'                         (.  inst = new Instruction("", "Div"); .)
  ).

/*------------------------------------------------------------------------*/

ProcDecl                        (.  string name; string label; Scope currentScope = openScopes.Peek(); int enterInstLocation = 0; bool external = false; .)
= "func"
  Ident<out name>               (.
                                    currentScope.Push(new Symbol(name, (int)TastierKind.Proc, (int)TastierType.Undefined, openScopes.Count, -1, 0, 0));
                                    openScopes.Push(new Scope());
                                    currentScope = openScopes.Peek();
                                .)
  '(' ')'
  '{'                           (.
                                    program.Add(new Instruction("", "Enter 0"));
                                    enterInstLocation = program.Count - 1;
                                    label = generateProcedureName(name);
                                    string outstr = openProcedureDeclarations.Count == 0 ? "[Global " : "[Nested ";
                                    Console.WriteLine(outstr + "Procedure]\t[Ident: " + name + "]\t[Label: " + label + "]");
                                    openProcedureDeclarations.Push(name);
                                    /*
                                      Enter is supposed to have as an
                                      argument the next free address on the
                                      stack, but until we know how many
                                      local variables are in this procedure,
                                      we don't know what that is. We'll keep
                                      track of where we put the Enter
                                      instruction in the program so that
                                      later, when we know how many spaces on
                                      the stack have been allocated, we can
                                      put the right value in.
                                    */
                                .)

  { ConstDecl |
    VarDecl<external> | Stat |  (.
                                    openLabels.Push(generateLabel());
                                    program.Add(new Instruction("", "Jmp " + openLabels.Peek()));
                                    /*
                                      We need to jump over procedure
                                      definitions because otherwise we'll
                                      execute all the code inside them!
                                      Procedures should only be entered via
                                      a Call instruction.
                                    */
                                .)
    ProcDecl                    (. program.Add(new Instruction(openLabels.Pop(), "Nop")); .)
  }
  '}'                           (.
                                    program.Add(new Instruction("", "Leave"));
                                    program.Add(new Instruction("", "Ret"));
                                    openScopes.Pop();
                                    // now we can generate the Enter instruction properly
                                    program[enterInstLocation] =
                                      new Instruction(label, "Enter " +
                                                      currentScope.Count(s => s.Item2 != (int)TastierKind.Proc));
                                    openProcedureDeclarations.Pop();
                                .).

/*------------------------------------------------------------------------*/

RelOp<out Instruction inst>
=                               (.  inst = new Instruction("", "Equ"); .)
  ( '='
  | "!="                        (.  inst = new Instruction("", "Neq"); .)
  | '<'                         (.  inst = new Instruction("", "Lss"); .)
  | "<="                        (.  inst = new Instruction("", "Lseq"); .)
  | '>'                         (.  inst = new Instruction("", "Gtr"); .)
  | ">="                        (.  inst = new Instruction("", "Gteq"); .)
  ).

/*------------------------------------------------------------------------*/

SimExpr<out TastierType type>   (.  TastierType type1; Instruction inst; .)
= Term<out type>
  { AddOp<out inst>
    Term<out type1>             (.
                                    if (type != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type);
                                    } else if (type1 != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type1);
                                    }
                                    program.Add(inst);
                                .)
  }.

/*------------------------------------------------------------------------*/

Stat                            (.
                                    TastierType type; string name; Symbol sym; bool external = false; bool isExternal = false;
                                    bool conditional = false; TastierType type1;
                                .)

= Ident<out name>               (.
                                    int arrayAccessDepth = 0;
                                    sym = lookup(openScopes, name);
                                    if (sym == null) {
                                      sym = _lookup(externalDeclarations, name);
                                      isExternal = true;
                                    }
                                    if (sym == null) {
                                      SemErr("reference to undefined variable " + name);
                                    }
                                .)
  (  ( '.' Ident<out name>      (.
                                    if ((TastierType) sym.Item3 != TastierType.Record) {
                                      // Can only use '.' syntax on record values
                                      TypeErr(TastierType.Record, (TastierType) sym.Item3);
                                    }
                                    Record rec = lookupRecordId(sym.Item7);
                                    if (memberExists(rec, name)) {
                                      sym = lookup(openScopes, "_" + sym.Item1 + "." + name);
                                    } else {
                                      SemErr(sym.Item1 + " does not contain member " + name);
                                    }
                                    // The type will be the type of the member
                                    type = (TastierType) sym.Item3;
                                .)

     | { '[' Expr<out type>     (.
                                    if (type != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type);
                                    }
                                    arrayAccessDepth++;
                                .)
         ']'
       }                        (.
                                    type = (TastierType) sym.Item3;
                                    if (arrayAccessDepth > 0) {
                                      if (arrayAccessDepth == sym.Item7) {
                                        // If enough dimensions provided, the type of this value is the base type
                                        type = (TastierType) sym.Item6;
                                      } else if (arrayAccessDepth > sym.Item7) {
                                        SemErr("Too many dimensions specified");
                                      }
                                    }
                                .)
     )
    ":="                        (.
                                    if ((TastierKind)sym.Item2 == TastierKind.Const) {
                                      SemErr("constant identifier " + name + " cannot be reassigned");
                                    } else if ((TastierKind)sym.Item2 != TastierKind.Var) {
                                      SemErr("cannot assign to non-variable");
                                    }
                                .)
  Expr<out type1>
  [ '?'                         (.
                                    conditional = true;
                                    if (type1 != TastierType.Boolean) {
                                      TypeErr(TastierType.Boolean, type);
                                    }
                                    string midLabel = generateLabel(); // label to jump to assign second expression
                                    program.Add(new Instruction("", "FJmp " + midLabel));
                                .)
    Expr<out type1>              (.
                                    if (type1 != type) {
                                      TypeErr(type, type1);
                                    }
                                    string endLabel = generateLabel();
                                    program.Add(new Instruction("", "Jmp " + endLabel));
                                    program.Add(new Instruction(midLabel, "Nop"));
                                .)
    ':'
    Expr<out type1>              (.
                                    if (type1 != type) {
                                      TypeErr(type, type1);
                                    }
                                    program.Add(new Instruction(endLabel, "Nop"));
                                .)
  ]
  ';'                           (.
                                    if (!conditional && type1 != type) {
                                      TypeErr(type, type1);
                                    }
                                    if ((TastierType) sym.Item3 == TastierType.Record) {
                                      // Reassign all record fields
                                      Record rec = lookupRecordId(sym.Item7);
                                      foreach (RecordVar member in rec.Item2) {
                                        Symbol memberSym = lookup(openScopes, "_" + sym.Item1 + "." + member.Item1);
                                        if (memberSym.Item4 == 0) {
                                          program.Add(new Instruction("", "StoG " + (memberSym.Item5+3)));
                                        } else {
                                          int lexicalLevelDifference = Math.Abs(openScopes.Count - memberSym.Item4)-1;
                                          program.Add(new Instruction("", "Sto " + lexicalLevelDifference + " " + memberSym.Item5));
                                        }
                                      }
                                    } else if (arrayAccessDepth == 0) {
                                      if (sym.Item4 == 0) {
                                        program.Add(new Instruction("", "StoG " + (sym.Item5+3)));
                                      } else {
                                        int lexicalLevelDifference = Math.Abs(openScopes.Count - sym.Item4)-1;
                                        program.Add(new Instruction("", "Sto " + lexicalLevelDifference + " " + sym.Item5));
                                      }
                                    } else {
                                      // Array assignment, first we must put the base address of the array on the stack
                                      if (sym.Item4 == 0) {
                                        if (isExternal) {
                                          program.Add(new Instruction("", "LoadG " + sym.Item1));
                                          // if the symbol is external, we load it by name. The linker will resolve the name to an address.
                                        } else {
                                          program.Add(new Instruction("", "LoadG " + (sym.Item5+3)));
                                        }
                                      } else {
                                        int lexicalLevelDifference = Math.Abs(openScopes.Count - sym.Item4)-1;
                                        program.Add(new Instruction("", "Load " + lexicalLevelDifference + " " + sym.Item5));
                                      }
                                      // Store the value at stack - 1 at memory address stored at stack - 2
                                      program.Add(new Instruction("", "StoArr " + arrayAccessDepth));
                                    }
                                .)
  | '(' ')' ';'                 (.
                                    if ((TastierKind)sym.Item2 != TastierKind.Proc) {
                                      SemErr("object is not a procedure");
                                    }

                                    int currentStackLevel = openScopes.Count;
                                    int lexicalLevelDifference = Math.Abs(openScopes.Count - sym.Item4);
                                    string procedureLabel = getLabelForProcedureName(lexicalLevelDifference, sym.Item1);
                                    program.Add(new Instruction("", "Call " + lexicalLevelDifference + " " + procedureLabel));
                                .)
  )

| "if"
  '(' Expr<out type> ')'        (.
                                    if (type != TastierType.Boolean) {
                                      TypeErr(TastierType.Boolean, type);
                                    }
                                    openLabels.Push(generateLabel());
                                    program.Add(new Instruction("", "FJmp " + openLabels.Peek()));
                                .)
      Stat                      (.
                                    Instruction startOfElse = new Instruction(openLabels.Pop(), "Nop");
                                    /*
                                       If we got into the "if", we need to
                                       jump over the "else" so that it
                                       doesn't get executed.
                                    */
                                    openLabels.Push(generateLabel());
                                    program.Add(new Instruction("", "Jmp " + openLabels.Peek()));
                                    program.Add(startOfElse);
                                .)
      [ "else"
        Stat
      ]                         (.  program.Add(new Instruction(openLabels.Pop(), "Nop")); .)

| "while"                       (.  string loopStartLabel = generateLabel();
                                    openLabels.Push(generateLabel()); //second label is for the loop end
                                    program.Add(new Instruction(loopStartLabel, "Nop"));
                                .)
  '(' Expr<out type> ')'        (.
                                    if ((TastierType)type != TastierType.Boolean) {
                                      SemErr("boolean type expected");
                                    }
                                    program.Add(new Instruction("", "FJmp " + openLabels.Peek())); // jump to the loop end label if condition is false
                                .)
      Stat                      (.  program.Add(new Instruction("", "Jmp " + loopStartLabel));
                                    program.Add(new Instruction(openLabels.Pop(), "Nop")); // put the loop end label here
                                .)
| "for"
  '(' Stat                      (.
                                    string updateLabel = generateLabel();
                                    string condLabel = generateLabel();
                                    string endLabel = generateLabel();
                                    program.Add(new Instruction("", "Jmp " + condLabel));
                                    program.Add(new Instruction(updateLabel, "Nop"));
                                .)
  Stat                          (.
                                    program.Add(new Instruction(condLabel, "Nop"));
                                .)
  Expr<out type>
  ')' "do"                      (.
                                    if (type != TastierType.Boolean) {
                                      TypeErr(TastierType.Boolean, type);
                                    }
                                    program.Add(new Instruction("", "FJmp " + endLabel));
                                .)
  Stat                          (.
                                    program.Add(new Instruction("", "Jmp " + updateLabel));
                                    program.Add(new Instruction(endLabel, "Nop"));
                                .)

| "read"
  Ident<out name> ';'           (.
                                    sym = lookup(openScopes, name);
                                    if (sym == null) {
                                      sym = _lookup(externalDeclarations, name);
                                      isExternal = true;
                                    }
                                    if (sym == null) {
                                      SemErr("reference to undefined variable " + name);
                                    }

                                    if ((TastierKind)sym.Item2 == TastierKind.Const) {
                                      SemErr("constant identifier " + name + " cannot be reassigned");
                                    } else if (sym.Item2 != (int)TastierKind.Var) {
                                      SemErr("variable type expected but " + sym.Item1 + " has kind " + (TastierType)sym.Item2);
                                    }

                                    if ((TastierType) sym.Item3 != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, (TastierType) sym.Item3);
                                    }
                                    program.Add(new Instruction("", "Read"));

                                    if (sym.Item4 == 0) {
                                      if (isExternal) {
                                        program.Add(new Instruction("", "StoG " + sym.Item1));
                                        // if the symbol is external, we also store it by name. The linker will resolve the name to an address.
                                      } else {
                                        program.Add(new Instruction("", "StoG " + (sym.Item5+3)));
                                      }
                                    }
                                    else {
                                      int lexicalLevelDifference = Math.Abs(openScopes.Count - sym.Item4)-1;
                                      program.Add(new Instruction("", "Sto " + lexicalLevelDifference + " " + sym.Item5));
                                    }
                                .)

| "write"
  Expr<out type>                (.
                                    EmitWrite(type);
                                .)
  { ',' Expr<out type>          (.
                                    EmitWrite(type);
                                .)
  } ';'

| SwitchStatement
| '{' { Stat | VarDecl<external> | ConstDecl } '}' .

/*------------------------------------------------------------------------*/

SwitchStatement                 (.  TastierType type; string nextCaseLabel; string caseLabel = null; .)
= "switch"
  '(' Expr<out type> ')' '{'   (.
                                    // The label that we will exit from
                                    string endLabel = generateLabel();
                               .)
  {
    Case<type, endLabel, caseLabel, out nextCaseLabel>
                                (.
                                    // The label for the body of the next case. Used for falling through
                                    caseLabel = nextCaseLabel;
                                .)
  }

  '}'                           (.
                                    program.Add(new Instruction(endLabel, "Nop"));
                                    // Remove duplicated value of initial expression
                                    program.Add(new Instruction("", "Pop"));
                                .).

/*------------------------------------------------------------------------*/

Case<TastierType type, string endLabel, string caseLabel, out string nextCaseLabel>
                                (.  TastierType exprType; bool fallThrough = false; string nextLabel = generateLabel(); .)
= ("case"                       (.
                                    // Clone top of stack so we can use it in the next "case"
                                    program.Add(new Instruction("", "Dup"));
                                .)
   Expr<out exprType>           (.
                                    if (type != exprType) {
                                      TypeErr(type, exprType);
                                    } else {
                                      program.Add(new Instruction("", "Equ"));
                                      // If this case doesn't match jump to the next comparison
                                      program.Add(new Instruction("", "FJmp " + nextLabel));
                                    }
                                .)
  |"default"
  ) ':'                         (.
                                    if (caseLabel != null) {
                                      program.Add(new Instruction(caseLabel, "Nop"));
                                    }
                                .)
  {Stat}
  [
    "fallthrough" ';'           (.
                                    fallThrough = true;
                                .)
  ]                             (.
                                    nextCaseLabel = generateLabel();

                                    if (!fallThrough) {
                                      // Not falling through, exit switch statement
                                      program.Add(new Instruction("", "Jmp " + endLabel));
                                    } else {
                                      // Falling through, jump to the body of the next case
                                      program.Add(new Instruction("", "Jmp " + nextCaseLabel));
                                    }
                                    // Next comparison
                                    program.Add(new Instruction(nextLabel, "Nop"));
                                .).

/*------------------------------------------------------------------------*/

Tastier                         (.  string name; bool external = false; .)
= "program"
  Ident<out name>               (.
                                    openScopes.Push(new Scope());
                                .)
  '{'
  { ConstDecl | VarDecl<external> | (.
                                    openLabels.Push(generateLabel());
                                    program.Add(new Instruction("", "Jmp " + openLabels.Peek()));
                                    /*
                                      We need to jump over procedure
                                      definitions because otherwise we'll
                                      execute all the code inside them!
                                      Procedures should only be entered via
                                      a Call instruction.
                                    */
                                .)
    ProcDecl                    (. program.Add(new Instruction(openLabels.Pop(), "Nop")); .)
  | ExternDecl | RecordDecl }
  '}'                           (.
                                    if (openScopes.Peek().Count == 0) {
                                      Warn("Warning: Program " + name + " is empty ");
                                    }

                                    header.Add(new Instruction("", ".names " + (externalDeclarations.Count + openScopes.Peek().Count)));
                                    foreach (Symbol s in openScopes.Peek()) {
                                      if (s.Item2 == (int)TastierKind.Var) {
                                        header.Add(new Instruction("", ".var " + ((int)s.Item3) + " " + s.Item1));
                                      } else if (s.Item2 == (int)TastierKind.Proc) {
                                        header.Add(new Instruction("", ".proc " + s.Item1));
                                      } else if (s.Item2 == (int)TastierKind.Const) {
                                        header.Add(new Instruction("", ".const " + s.Item1));
                                      } else {
                                        SemErr("global item " + s.Item1 + " has no defined type");
                                      }
                                    }
                                    foreach (Symbol s in externalDeclarations) {
                                      if (s.Item2 == (int)TastierKind.Var) {
                                        header.Add(new Instruction("", ".external var " + ((int)s.Item3) + " " + s.Item1));
                                      } else if (s.Item2 == (int)TastierKind.Proc) {
                                        header.Add(new Instruction("", ".external proc " + s.Item1));
                                      } else {
                                        SemErr("external item " + s.Item1 + " has no defined type");
                                      }
                                    }
                                    header.AddRange(program);
                                    openScopes.Pop();
                                .).

/*------------------------------------------------------------------------*/

Term<out TastierType type>      (.  TastierType type1; Instruction inst; .)
= Factor<out type>
  { MulOp<out inst>
    Factor<out type1>           (.
                                    if (type != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type);
                                    } else if (type1 != TastierType.Integer) {
                                      TypeErr(TastierType.Integer, type1);
                                    }
                                    program.Add(inst);
                                .)
  }.

/*------------------------------------------------------------------------*/

Type<out TastierType type>
=                               (.  type = TastierType.Undefined; .)
 ( "int"                        (.  type = TastierType.Integer; .)
 | "bool"                       (.  type = TastierType.Boolean; .)
 | "string"                     (.  type = TastierType.String; .)
 | "char"                       (.  type = TastierType.Character; .)
 ).

/*------------------------------------------------------------------------*/

ConstDecl                       (.
                                    string name; TastierType type; TastierType exprType;
                                    Symbol sym; Scope currentScope = openScopes.Peek();
                                    string outstr = openScopes.Count == 1 ? "[Global Constant]\t" : "[Constant]\t\t";
                                .)
= "const"
  Type<out type>
  Ident<out name>               (.
                                    int count = currentScope.Count(s => s.Item2 != (int)TastierKind.Proc);
                                    Console.WriteLine(outstr + "[Ident: " + name + "]\t[Type: " + type + "]\t[Scope: " + (openScopes.Count - 1) + "]\t[Address: " + count + "]");
                                    sym = new Symbol(name, (int)TastierKind.Const, (int)type, openScopes.Count - 1, count, 0, 0);
                                    currentScope.Push(sym);
                                .)
  ":=" Expr<out exprType> ';'
                                (.
                                    if (type != exprType) {
                                      TypeErr(type, exprType);
                                    }
                                    if (openScopes.Count == 1) {
                                      program.Add(new Instruction("", "StoG " + (sym.Item5 + 3)));
                                    } else {
                                      program.Add(new Instruction("", "Sto 0 " + sym.Item5));
                                    }
                                .).

/*------------------------------------------------------------------------*/

VarDecl<bool external>          (.
                                    string name;
                                    TastierType type = TastierType.Undefined;
                                    Scope currentScope = openScopes.Peek();
                                    string outstr = openScopes.Count == 1 ? "[Global Variable]\t" : "[Variable]\t\t";
                                    TastierType arrayType = TastierType.Undefined;
                                    int arrayDimensions = 0;
                                    List<int> lengths = new List<int>();
                                    int dimensionRequired = 0;
                                    bool recordDecl = false;
                                    Record rec = null;
                                .)
= ( Type<out type>
    {
                                // Here we can either initialise an array with dimensions
                                // or leave them as optional
      ( '[' number              (.
                                    arrayDimensions++;
                                    if (dimensionRequired == 2) {
                                      SemErr("Previous dimensions not specified");
                                    }
                                    int length = Convert.ToInt32(t.val);
                                    if (length <= 0) {
                                      SemErr("Array length cannot be <= 0");
                                    }
                                    lengths.Add(length);
                                    dimensionRequired = 1;
                                    if (arrayType == TastierType.Undefined) {
                                      arrayType = type;
                                      type = TastierType.Array;
                                    }
                                .)
        ']'
      | "[]"                    (.
                                    arrayDimensions++;
                                    if (dimensionRequired == 1) {
                                      if (arrayDimensions != lengths.Count) {
                                        SemErr("Dimension must be specified");
                                      }
                                    } else {
                                      dimensionRequired = 2;
                                    }
                                    if (arrayType == TastierType.Undefined) {
                                      arrayType = type;
                                      type = TastierType.Array;
                                    }
                                .)
      )
    }
  | "record" Ident<out name>    (.
                                    recordDecl = true;
                                    if (recordDeclarations.ContainsKey(name)) {
                                      rec = recordDeclarations[name];
                                      type = TastierType.Record;
                                      // Use array dimensions field as storage for the id of the record
                                      arrayDimensions = rec.Item1;
                                    } else {
                                      SemErr("Record " + name + " does not exist");
                                    }
                                .)
  )
  Ident<out name>               (.
                                    int count = currentScope.Count(s => s.Item2 != (int)TastierKind.Proc);
                                    Console.WriteLine(outstr + "[Ident: " + name + "]\t[Type: " + type + "]\t[Scope: " + (openScopes.Count - 1) + "]\t[Address: " + count + "]");
                                    Symbol sym = null;
                                    if (external) {
                                      sym = new Symbol(name, (int)TastierKind.Var, (int)type, 0, 0, (int) arrayType, arrayDimensions);
                                      externalDeclarations.Push(sym);
                                    } else {
                                      sym = new Symbol(name, (int)TastierKind.Var, (int)type, openScopes.Count-1, currentScope.Count(s => s.Item2 != (int)TastierKind.Proc), (int) arrayType, arrayDimensions);
                                      currentScope.Push(sym);
                                    }
                                    if (lengths.Count != 0) {
                                      AllocateArray(lengths);
                                      if (openScopes.Count == 1) {
                                        program.Add(new Instruction("", "StoG " + (sym.Item5 + 3)));
                                      } else {
                                        program.Add(new Instruction("", "Sto 0 " + sym.Item5));
                                      }
                                    }
                                    if (recordDecl) {
                                      // Create symbols for each member of this record instance
                                      foreach (RecordVar member in rec.Item2) {
                                        currentScope.Push(new Symbol("_" + name + "." + member.Item1, (int) TastierKind.Var, member.Item2, openScopes.Count-1, currentScope.Count(s => s.Item2 != (int)TastierKind.Proc), member.Item3, member.Item4));
                                      }
                                    }
                                .)
  { ',' Ident<out name>         (.
                                    count = currentScope.Count(s => s.Item2 != (int)TastierKind.Proc);
                                    Console.WriteLine(outstr + "[Ident: " + name + "]\t[Type: " + type + "]\t[Scope: " + (openScopes.Count - 1) + "]\t[Address: " + count + "]");
                                    if (external) {
                                      sym = new Symbol(name, (int)TastierKind.Var, (int)type, 0, 0, (int) arrayType, arrayDimensions);
                                      externalDeclarations.Push(sym);
                                    } else {
                                      sym = new Symbol(name, (int)TastierKind.Var, (int)type, openScopes.Count-1, currentScope.Count(s => s.Item2 != (int)TastierKind.Proc), (int) arrayType, arrayDimensions);
                                      currentScope.Push(sym);
                                    }
                                    if (lengths.Count != 0) {
                                      AllocateArray(lengths);
                                      if (openScopes.Count == 1) {
                                        program.Add(new Instruction("", "StoG " + (sym.Item5 + 3)));
                                      } else {
                                        program.Add(new Instruction("", "Sto 0 " + sym.Item5));
                                      }
                                    }
                                    if (recordDecl) {
                                      // Create symbols for each member of this record instance
                                      foreach (RecordVar member in rec.Item2) {
                                        currentScope.Push(new Symbol("_" + name + "." + member.Item1, (int) TastierKind.Var, member.Item2, openScopes.Count-1, currentScope.Count(s => s.Item2 != (int)TastierKind.Proc), member.Item3, member.Item4));
                                      }
                                    }
                                .)
  } ';'.

/*------------------------------------------------------------------------*/

ExternDecl                      (.  string name; bool external = true; Scope currentScope = openScopes.Peek(); int count = currentScope.Count; .)
= "external"
  ( VarDecl<external>
  | "procedure" Ident<out name> ';'
                                (.  externalDeclarations.Push(new Symbol(name, (int)TastierKind.Proc, (int)TastierType.Undefined, 1, -1, 0, 0)); .)
  ).

/*------------------------------------------------------------------------*/

RecordDecl                      (.
                                    TastierType type;
                                    string name;
                                    string memberName;
                                .)
= "define"
  "record" Ident<out name> '{'  (.  List<RecordVar> variables = new List<RecordVar>(); .)
  {
    Type<out type>              (.
                                    TastierType arrayType = TastierType.Undefined;
                                    int arrayDepth = 0;
                                .)
    {
      "[]"                      (.
                                    arrayDepth++;
                                    if (arrayType == TastierType.Undefined) {
                                      arrayType = type;
                                      type = TastierType.Array;
                                    }
                                .)
    }
    Ident<out memberName> ';'   (.
                                    variables.Add(new RecordVar(memberName, (int) type, (int) arrayType, arrayDepth));
                                .)
  }
  '}'
                                (.
                                    Record record = new Record(recordsDeclared++, variables);
                                    recordDeclarations.Add(name, record);
                                .).

END Tastier.
